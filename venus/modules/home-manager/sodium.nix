# Main hm module for Sodium
{ config, pkgs, lib, nixpkgs, ... }:
{
  imports = [
    ./venus-location.nix
    ./legacy-base.nix
    ./use-jujutsu.nix
    ./use-yt-dlp.nix
    ./use-fish.nix
    #./use-halloy.nix
    ./use-mpv.nix
    ./use-direnv.nix
    ./use-tealdeer.nix
    ./use-resticprofile.nix
    ./ooss-maker.nix
    ./use-manuals.nix
    ./../../../magic/home-manager/module.nix
  ];
  config = {
    venus.ooss-maker-for-this-system = config.lib.file.mkOOSS-Sodium;
    use-fish.enableHomebrewEscapeHatch = true;
    use-fish.enableDarwinPathWorkaround = true;
    venus.use-mpv.enableDarwinAppBundleWorkaround = true;

    home.packages = [
      pkgs.borgbackup
      pkgs.iina
      pkgs.ffmpeg-full
      pkgs.terminal-notifier
      pkgs.comma
      pkgs.lsd
      pkgs.jellyfin-mpv-shim # TODO: fix this
      pkgs.devenv
      pkgs.chaseln
      #pkgs.cinny-desktop
      pkgs.element-desktop
      pkgs.neovim # Temporary until I figure out how to integrate ammonia into the sodium config
      pkgs.check-gits
      pkgs.rclone
      pkgs.audacity
      pkgs.kubectl
      pkgs.claude-code
    ];

    home.sessionVariables = {
      MANPAGER = "${pkgs.neovim}/bin/nvim +Man!";
      #MANPAGER = "${pkgs.vim}/bin/vim +Man!"; # Doesn't work; you need to run `:runtime! ftplugin/main.vim<CR>:Man ` followed by the keyword
      #MANPAGER = "${pkgs.nvimpager}/bin/nvimpager";
      #MANPAGER = "${pkgs.bat-extras.batman}/bin/batman";
      EDITOR = "${pkgs.neovim}/bin/nvim";
    };

    home.shellAliases = {
      # This assumes this repo is in ~/src/venus
      sdrs = "sudo darwin-rebuild switch --flake ${config.magic.absolutePathStrings.sodium.everythingRepo}/flake-profiles/system-sodium";
      # TODO: package makemkv
      maybe-makemkvcon = "/Applications/MakeMKV.app/Contents/MacOS/makemkvcon";
      maybe-pulumi = "/opt/homebrew/bin/pulumi";
      maybe-code = "/opt/homebrew/bin/code";
      maybe-love = "/Applications/love.app/Contents/MacOS/love";

      "maybe-NH₂OH" = "nix run ${config.home.homeDirectory}/src/hydroxylamine";
      maybe-hydroxylamine = "maybe-NH₂OH";

      maybe-ammonia = "nix run ${config.home.homeDirectory}/src/ammonia";
      maybe-ammonia-cached = "${config.home.homeDirectory}/src/ammonia/result/bin/nvim";

      ban = ''BAT_THEME="Monokai Extended" ${pkgs.bat-extras.batman}/bin/batman''; # Select dark theme for dark alacritty
    };

    home.file.".ssh/config".source = config.venus.ooss-maker-for-this-system "ssh-config.txt";

    # TODO: this is coupled with jellyfin-mpv-shim
    # jellyfin-mpv-shim uses its own directory to store its configs. It does NOT load scripts from the vanilla mpv config directory.
    # v This is a symlink to the vanilla mpv config file.
    # We use a non-hotlink symlink because the config file is generated by home-manager.
    # The path depends on the value of the config directory (private variable declared https://github.com/nix-community/home-manager/blob/2f23fa308a7c067e52dfcc30a0758f47043ec176/modules/misc/xdg.nix#L15)
    # and the hardcoded mpv.conf path (https://github.com/nix-community/home-manager/blob/2f23fa308a7c067e52dfcc30a0758f47043ec176/modules/programs/mpv.nix#L209)
    home.file."Library/Application Support/jellyfin-mpv-shim/mpv.conf".source = config.lib.file.mkOutOfStoreSymlink "${config.home.homeDirectory}/.config/mpv/mpv.conf";
    # v This is another config file similar to the above.
    # At the time of writing this, `input.conf` isn't actually generated because I'm not using that option in home-manager.
    home.file."Library/Application Support/jellyfin-mpv-shim/input.conf".source = config.lib.file.mkOutOfStoreSymlink "${config.home.homeDirectory}/.config/mpv/input.conf";
    # v This is another symlink similar to the above, but for a whole directory.
    # Perhaps in the future, I will be more granular and link file-by-file (so that some conf files can be used only for jellyfin-mpv-shim, and vice versa. This is apparently useful since not all scripts are usable in jellyfin-mpv-shim.)
    home.file."Library/Application Support/jellyfin-mpv-shim/script-opts".source = config.lib.file.mkOutOfStoreSymlink "${config.home.homeDirectory}/.config/mpv/script-opts";
    # v This is another symlink similar to the above, also for a whole directory.
    home.file."Library/Application Support/jellyfin-mpv-shim/scripts".source = config.lib.file.mkOutOfStoreSymlink "${config.home.homeDirectory}/.config/mpv/scripts";
    # v This is a hot-file config for jellyfin-mpv-shim. Since it is configurable via GUI, it needs to be out of store.
    home.file."Library/Application Support/jellyfin-mpv-shim/conf.json".source = config.venus.ooss-maker-for-this-system "jellyfin-mpv-shim-conf.json";

    # TODO: clean this up
    xdg.configFile."ghostty/config".source = config.venus.ooss-maker-for-this-system "ghostty-config";
    
    # TODO: clean this up
    home.file.".emacs.d/init.el".source = config.lib.file.mkOutOfStoreSymlink "${config.home.homeDirectory}/src/cyanide/result/init-file/init.el";

    services.syncthing = {
      enable = true;
      #tray = {
      #  enable = true;
      #};
      overrideDevices = false;
      overrideFolders = false;

      # INFO: doesn't seem to do anything
      #passwordFile = builtins.trace "pw file is here ${config.sops.secrets.syncthing_pw.path}" config.sops.secrets.syncthing_pw.path;
    };

    sops = {
      defaultSopsFile = ./../../../secrets/example.yaml;
      age.keyFile = "${config.magic.absolutePathStrings.sodium.home}/.config/sops/age/keys.txt";
      secrets.example_key = { };
      secrets.main_restic_backups = {
        sopsFile = ./../../../secrets/personal/restic-repo-passwords.yaml;
        path = "${config.magic.absolutePathStrings.sodium.home}/.config/resticprofile/password.txt";
      };
      secrets.syncthing_pw = {
        sopsFile = ./../../../secrets/personal/passwords.yaml;
      };
      secrets."personal/rclone.conf.ini" = {
        format = "ini";
        mode = "0400"; # Of the regular unenc file in ~/.config/sops-nix/secrets/personal/rclone.conf.ini
        sopsFile = ./../../../secrets/personal/rclone.conf.ini; # Override the defaultSopsFile
        path = "${config.magic.absolutePathStrings.sodium.home}/.config/rclone/rclone.conf"; # Symlink to the regular unenc file
      };
      secrets."personal/jupiter.env" = {
        format = "dotenv";
        mode = "0400";
        sopsFile = ./../../../secrets/personal/jupiter.env;
        path = "${config.magic.absolutePathStrings.sodium.home}/src/gas-giants/jupiter/.env";
      };
    };
  };
}
